plugins {
    id 'java'
    id "me.champeau.jmh" version "0.7.2"
    id "io.morethan.jmhreport" version "0.9.0"
    id 'org.graalvm.buildtools.native' version '0.9.28'
    id "com.google.osdetector" version "1.7.3"
}

group = 'com.github.lhotari'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    performanceTest
}

configurations {
    performanceTestImplementation.extendsFrom(testImplementation)
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.1')
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    performanceTestImplementation project(':')
}

test {
    outputs.upToDateWhen { false }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

def performanceTest = tasks.register("performanceTest", Test) {
    testClassesDirs = sourceSets.performanceTest.output.classesDirs
    classpath = sourceSets.performanceTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

graalvmNative {
    registerTestBinary("performanceTest") {
        usingSourceSet(sourceSets.performanceTest)
        forTestTask(performanceTest)
    }
}

jmh {
    resultsFile = layout.buildDirectory.file("reports/jmh/results.json")
    resultFormat = "json"
    if (project.hasProperty("jfrProfiling")) {
        profilers = ["jfr:dir=${project.file("build/reports/jmh")};configName=profile"]
    } else if (project.hasProperty("asyncProfiling")) {
        // https://github.com/async-profiler/async-profiler#basic-usage
        // On Linux, you also need to execute
        // sudo sysctl kernel.perf_event_paranoid=1
        // sudo sysctl kernel.kptr_restrict=0
        profilers = ["async:libPath=${project.file("lib/async-profiler/${osdetector.os}/libasyncProfiler.so")};event=cpu;output=flamegraph;dir=${project.file("build/reports/jmh")}"]
    }
}

jmhReport {
    jmhResultPath = project.file("build/reports/jmh/results.json")
    jmhReportOutput = project.file("build/reports/jmh")
}